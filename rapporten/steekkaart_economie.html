<!DOCTYPE html>
<html lang="nl-NL">
<!-- todo:
- horizontale pagina's in PDF
-->

<!-- door je instellingen en selecties tussen "fake HTML" te zetten, kan je deze eenvoudig samenklikken, dit werkt veel handiger -->
<inputs>#
input_geo=input
{
    Type: Geo;
    Caption: "Kies een gebied";
    Instruction: "Kies het gebied dat je interesseert.";
    Level: [Geo.gemeente];
    Min: 1;
    Max: 1;

}
input_compare=input
{
    Type: GeoItem;
    Caption: "Kies een vergelijkingsgebied";
    Instruction: "Kies het gebied waarmee je wil vergelijken.";
    Subset: parents(input_geo);
    Default: [provincie._99999];
    Min: 1;
    Max: 1;
}

inwoners = Presentation {
	Selection {
		[Ind.v1101_t];
		MostRecentPeriod: 1;
		input_geo;
	};
	View: Table;
	ViewDimensions: [dim_variable];
}


#<!-- maakt het gemakkelijker om het gekozen gebiedsniveau aan te spreken -->#
geolevel = level(item(input_geo, 0))

#<!-- telt het aantal gekozen gebieden -->#
count = Presentation {
        Selection {
                Ind.v1101_m;
                MostRecentPeriod: 1;
                input_geo;
        };
        View: Table;
}

#
</inputs>
<selections>
#
    introkaart = Presentation {
	Selection {
		Ind.v1101_m;
		MostRecentPeriod: 1;
		input_compare;
		SplitUp: geolevel;
	};
	View: Map;
	ViewDimensions: [dim_geo, dim_variable];
	LayoutClass: "introkaart";
	FavoriteGeoItem: item(input_geo,0);
}

introkaart2 = Presentation {
	Selection {
		[Ind.v1101_m];
		MostRecentPeriod: 1;
		input_geo;
		compare: item(input_compare,0);
        CompareShowGeoIndividual: true;
        CompareShowGeoTotal: false;		
	};
	View: Map;
	ViewDimensions: [dim_geocompare, dim_variable, dim_period];
		LayoutClass: "introkaart";
}

absolutegroeibevolking = Presentation {
	Selection {
		Ind.v1101_t;
		[Per._1991, Per._1992, Per._1993, Per._1994, Per._1995, Per._1996, Per._1997, Per._1998, Per._1999, Per._2000, Per._2001, Per._2002, Per._2003, Per._2004, Per._2005, Per._2006, Per._2007, Per._2008, Per._2009, Per._2010, Per._2011, Per._2012, Per._2013, Per._2014, Per._2015, Per._2016, Per._2017];
		input_geo;
		compare: item(input_compare,0);
		Transformation: Growth;
		BasePeriod: per._1991;
		CompareShowGeoIndividual: false;
        CompareShowGeoTotal: true;
	};
	View: Line;
	ViewDimensions: [dim_geocompare, dim_period, dim_variable];
}


absolutegroeibevolking_laagniveau = Presentation {
	Selection {
		Ind.v1101_t;
		[Per._2005, Per._2006,Per._2007, Per._2008, Per._2009, Per._2010, Per._2011, Per._2012, Per._2013, Per._2014, Per._2015, Per._2016, Per._2017];
		input_geo;
		compare: item(input_compare,0);
		Transformation: Growth;
		BasePeriod: per._2005;
		CompareShowGeoIndividual: false;
        CompareShowGeoTotal: true;
	};
	View: Line;
	ViewDimensions: [dim_geocompare, dim_period, dim_variable];
}

pres_a = Presentation {
	Selection {
		[Ind.vp1101_0_19, Ind.vp1101_20_39, Ind.vp1101_40_59, Ind.vp1101_60_79, Ind.vp1101_80p];
		MostRecentPeriod: 1;
		input_geo;
		compare: item(input_compare,0);
        CompareShowGeoIndividual: false;
        CompareShowGeoTotal: true;		
	};
	View: ProfileBar;
	ViewDimensions: [dim_geocompare, dim_variable, dim_period];
	LayoutClass: "pres_a";
}

vis_1 = Presentation {
	Selection {
		Ind.vp1501_bedrijvigheid;
		MostRecentPeriod: 1;
		Descendants(parent(input_geo, provincie), gemeente);
		compare: input_compare;	
		};
	View: Map;
	ViewDimensions: [dim_geo, dim_variable];
	LayoutClass: "vis_1";
	FavoriteGeoItem: item(input_geo,0);
}

vis_2 = Presentation {
	Selection {
		Ind.v1203_welvaartsindex;
		MostRecentPeriods(Ind.v1203_welvaartsindex, year, 10);
		input_geo;
		compare: item(input_compare);
		BasePeriod: Item(MostRecentPeriods(Ind.v1203_welvaartsindex, year, 10), 9);
	};
	View: Line;
	ViewDimensions: [dim_geocompare, dim_period, dim_variable];
	LayoutClass: "vis_2";
}


vis_3 = Presentation {
	Selection {
		[Ind.v1201_ksz_loontrekkend, Ind.v1201_ksz_zelfstandige, Ind.v1201_ksz_werkzoekend, Ind.v1201_ksz_pensioen, Ind.v1201_ksz_kinderbijslag, Ind.v1201_ksz_overige];
		MostRecentPeriods(Ind.v1201_ksz_kinderbijslag, year, 1);
		input_geo;
	};
	View: Pie;
	ViewDimensions: [dim_variable, dim_period];
	LayoutClass: "vis_3";
}

vis_4 = Presentation {
	Selection {
		Ind.v1321_wlhgraad_t;
		MostRecentPeriods(Ind.v1321_wlhgraad_t, month, 216);
		input_geo;
		compare: item(input_compare);
	};
	View: Line;
	ViewDimensions: [dim_geocompare, dim_period, dim_variable];
	LayoutClass: "vis_4";
}

vis_5 = Presentation {
	Selection {
		Ind.v1402_act_onderneming;
		MostRecentPeriods(Ind.v1402_act_onderneming, year, 10);
		input_geo;
	};
	View: Bar;
	ViewDimensions: [dim_geo, dim_period, dim_variable];
}


vis_6 = Presentation {
	Selection {
		Ind.vp1601_pand;
		MostRecentPeriod: 1;
		Descendants(parent(input_geo, provincie), gemeente);
		compare: input_compare;	
		};
	View: Map;
	ViewDimensions: [dim_geo, dim_variable];
	LayoutClass: "vis_1";
	FavoriteGeoItem: item(input_geo,0);
}



#
</selections>    
<head>
<meta charset="utf-8" />
<title>Steekkaart Economie</title>
<link type="text/css" rel="stylesheet" href="style/dynamisch_rapport_printvriendelijk.css?011" />
</head>
<body class="swingreport">

<p>&nbsp;</p><p>&nbsp;</p>
<section class="container">
    <section class="hoofdstuk">
        <h1>Basisgegevens Economie in #Write(name(level(item(input_geo,0))))# #show(input_geo)#</h1>

<h2>Inhoud</h2>
<p>In dit rapport geven we enkele basiskenmerken van de Economie in #Write(name(level(item(input_geo,0))))# #show(input_geo)# weer.
Dit rapport is aangemaakt op basis van de data die we op #date()# ter beschikking hebben. Via de wijzig knop kunt u zelf een gebied kiezen en deze vergelijken met andere gebieden.
 </p>
 <p>&nbsp;</p>
<p>Een volledig overzicht van economische indicatoren zoals die door de Interprovinciale Werkgroep Economie gedefinieerd zijn 
<a href="https://provincies.incijfers.be/?report=rapport_economie&input_geo=#code(input_geo)#&keepworkspace=true" target="_top">vindt u in dit uitgebreide tabellenrapport</a>.
</p>
 <p>&nbsp;</p>
 
#if ListCount(Items(count, dim_geo))>1 then#
<p>#show(introkaart2, 750, 450)#</p>
# else #
    <p>
    #show(introkaart, 400, 330)#
    #show(introkaart2, 400, 330)#
    </p>
#end if#

    </section>

    <section class="hoofdstuk">
        <h2>Bevolking</h2>  
<p>&nbsp;</p>
<p>
    In de #Write(name(level(item(input_geo,0))))# <b>#show(input_geo)#</b> wonen in totaal <b>#Write(data(inwoners,0,0))#</b> mensen (#Write(mostrecentperiods(v1101_t,year,1))#).
</p>
    <p>De eerste grafiek toont de verhouding van de bevolking naar leeftijd in #Write(name(level(item(input_geo,0))))# #show(input_geo)# vergeleken met  #Write(name(level(item(input_compare,0))))# #show(input_compare)#</p>
 <p>&nbsp;</p>
 <p>
 #show (pres_a, 395, 425)#

 #if Equal(Code(level(item(input_geo,0))), "gemeente") OR Equal(Code(level(item(input_geo,0))), "provincie")  then#
#show(absolutegroeibevolking, 395, 425)#
 #else#
#show(absolutegroeibevolking_laagniveau, 395, 425)#
 #end if#
</p>

</section>

<section class="hoofdstuk">
    <h2>Ruimte voor bedrijvigheid</h2>  

<div class="zweefrechts">    
#show(vis_1, 600, 425)#
</div>
<br><br><p>
In de #Write(name(level(item(input_geo,0))))# #show(input_geo)# is <b> 
    #Show(data{Ind.vp1501_bedrijvigheid;Item(mostrecentperiods(vp1501_bedrijvigheid,year,1));item(input_geo,0);})#%</b>
    van de <a href="https://provincies.incijfers.be/jive/JiveObjectInfo.aspx?type=var&code=vp1501_bedrijvigheid" target="_blank">bebouwde oppervlakte bestemd voor bedrijvigheid</a>.<br><br>
    In #Write(name(level(item(input_compare,0))))# #show(input_compare,0)# is dat <b>#Show(data{Ind.vp1501_bedrijvigheid;Item(mostrecentperiods(vp1501_bedrijvigheid,year,1));item(input_compare,0);})#%</b>.
</p>

</section>

<section class="hoofdstuk">

<h2>Evolutie van het fiscaal inkomen </h2>
<div class="zweefrechts">#showInteractive(vis_2,600,425)#</div>
<p>&nbsp;</p>
<p>
De <a href="https://provincies.incijfers.be/jive/JiveObjectInfo.aspx?type=var&code=v1203_welvaartsindex" target="_blank">
    welvaartsindex</a> is gebaseerd op het fiscaal inkomen per persoon. Het vergelijkt het gemiddelde inkomen per inwoner 
    van een gebied met het gemiddelde van Belgi&euml;, jaar per jaar. Voor Belgi&euml; is de waarde steeds 100.
    Waar men gemiddeld 10% minder verdient, zou de waarde 90 zijn.</p>
<p>&nbsp;</p>
<p>De evolutie van deze waarde zegt dus enkel iets over de relatieve positie van #Write(name(level(item(input_geo,0))))# #show(input_geo)# 
en #Write(name(level(item(input_compare,0))))# #show(input_compare,0)# ten opzichte van Belgi&euml;.<br>
In absolute termen evolueerde het inkomen van de gemiddelde inwoner van #show(input_geo)# van <b>
#Show(data{Ind.v1203_gem_inwoner;Item(mostrecentperiods(v1203_gem_inwoner,year,11),10);item(input_geo,0);})# euro </b>
(#Write(Item(mostrecentperiods(v1203_gem_inwoner,year,11),10))#) naar <b>
#Show(data{Ind.v1203_gem_inwoner;Item(mostrecentperiods(v1203_gem_inwoner,year,1));item(input_geo,0);})# euro </b>
 (#Write(Item(mostrecentperiods(v1203_gem_inwoner,year,1)))#).
In #Write(name(level(item(input_compare,0))))# #show(input_compare,0)# is dit van 
#Show(data{Ind.v1203_gem_inwoner;Item(mostrecentperiods(v1203_gem_inwoner,year,11),10);item(input_compare,0);})# 
euro naar
#Show(data{Ind.v1203_gem_inwoner;Item(mostrecentperiods(v1203_gem_inwoner,year,1));item(input_compare,0);})# euro ge&euml;volueerd.</p>

</section>

<section class="hoofdstuk">
<h2>Inwoners volgens hun socio-economische positie (#show(Item(mostrecentperiods(v1201_ksz_kinderbijslag,year,1)))#)</h2>
<div class="zweefrechts">#showInteractive(vis_3,600,425)#</div>
<p>&nbsp;</p>
<p>&nbsp;</p>
<p>Op basis van de Kruispuntbank Sociale Zekerheid (Datawarehouse arbeidsmarkt en sociale bescherming) is het mogelijk om de inwoners van #Write(name(level(item(input_geo,0))))# #show(input_geo)#
te classificeren op basis van hun relatie tot de arbeidsmarkt.
</p>
<p>&nbsp;</p>
<p>Doordat de verwerking van de gegevens over kinderbijslag langer op zich laten wachten, kunnen we deze taart
 slechts tot en met #show(Item(mostrecentperiods(v1201_ksz_kinderbijslag,year,1)))# vol maken. U kunt 
 <a HREF="https://provincies.incijfers.be/jive?var=v1201_ksz_loontrekkend,v1201_ksz_zelfstandige,v1201_ksz_werkzoekend,v1201_ksz_pensioen,v1201_ksz_kinderbijslag,v1201_ksz_overige&geoitem_codes=#Write(Code(input_geo))#&view=barstacked&period=#Write(mostrecentperiods(v1201_ksz_loontrekkend,year,10))#&keepworkspace=true" target="_blank">
hier een evolutiegrafiek van de beschikbare cijfers opvragen</a>.
</p>
<p>&nbsp;</p>
<p> </p>
 
</section>

<section class="hoofdstuk">
<h2>Maandelijkse evolutie van de werkloosheidsgraad</h2>
<p>&nbsp;</p>
<p>In #Write(name(level(item(input_geo,0))))# #show(input_geo)# bedraagt de werkloosheidsgraad in 
#show(Item(mostrecentperiods(v1321_wlhgraad_t,month,1)))# #show(Item(mostrecentperiods(v1321_wlhgraad_t,year,1)))# zo'n 
<b>#Show(data{Ind.v1321_wlhgraad_t;Item(mostrecentperiods(v1321_wlhgraad_t,month,1));item(input_geo,0);})#%</b>. <br>
In #Write(name(level(item(input_compare,0))))# #show(input_compare,0)#
is dat <b>#Show(data{Ind.v1321_wlhgraad_t;Item(mostrecentperiods(v1321_wlhgraad_t,month,1));item(input_compare,0);})#%</b>.
</p>
<p>Deze cijfers komen van Arvastat. 
#if Equal(Code(level(item(input_geo,0))), "gemeente") then# 
Via <a href="https://arvastat.vdab.be/arvastat_basisstatistieken_werkloosheid.html?vlheader=N&header=N&geo=#subcode(input_geo)#" target="_blank">deze link</a> kunt u daar een uitgebreide versie van deze cijfers voor #show(input_geo)# raadplegen.
#end if#
Via Provincies in Cijfers kunt u zeer binnenkort profielen van werkzoekenden en zeer lokale cijfers raadplegen.</p>
<p>&nbsp;</p>
<p>
#
showInteractive(vis_4)
#</p>

</section>


<section class="hoofdstuk">

<h2>Evolutie van het aantal ondernemingen</h2>

<div class="zweefrechts">#showInteractive(vis_5,600,425)#</div>
<br><br><p>De statistiek van het 
    <a href="https://provincies.incijfers.be/jive/JiveObjectInfo.aspx?type=var&code=v1402_act_onderneming" target="_blank">aantal ondernemingen</a> 
    komt van Statbel, als telling van het aantal BTW-plichtigen.</p>
</section>



<section class="hoofdstuk">

<h2>Detailhandel</h2>

<div class="zweefrechts">    
#show(vis_6, 600, 425)#
</div>
<br><br><p>
In de #Write(name(level(item(input_geo,0))))# #show(input_geo)# zijn er <b> 
    #Show(data{Ind.vp1601_pand;Item(mostrecentperiods(vp1601_pand,year,1));item(input_geo,0);})#</b>
    <a href="https://provincies.incijfers.be/jive/JiveObjectInfo.aspx?type=var&code=vp1601_pand" target="_blank">handelspanden in brede zin</a> 
    per 1000 inwoners (#Write(Item(mostrecentperiods(vp1601_pand,year,1)))#).<br>
    In #Write(name(level(item(input_compare,0))))# #show(input_compare,0)# is dat <b>#Show(data{Ind.vp1601_pand;Item(mostrecentperiods(vp1601_pand,year,1));item(input_compare,0);})# per 1000 inwoners</b>.
</p>
<p>In 2008 was dit nog #Show(data{Ind.vp1601_pand;Per._2008;item(input_geo,0);})# 
(#Write(name(level(item(input_geo,0))))# #show(input_geo)#) en #Show(data{Ind.vp1601_pand;Per._2008;item(input_compare,0);})# (#Write(name(level(item(input_compare,0))))# #show(input_compare,0)#).
</p>
</section>

</section>
<p>&nbsp;</p>
</body>
</html>
